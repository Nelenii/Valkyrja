; Valkyrja AoE2 AI by Nelenii for AoE2 Definitive Edition

; ---- Constants ----

(defconst vkc_False 0)
(defconst vkc_True 1)

(defconst vkc_DebugLog_RuleError vkc_True)
(defconst vkc_DebugLog_LoopData vkc_True)

(defconst vkc_EarlyGame_WoodFoodGoldStone_MaxDropDistance   20)
(defconst vkc_EarlyGame_FishHunt_MaxDropDistance            30)
(defconst vkc_LateGame_WoodFoodGoldStone_MaxDropDistance    -1)
(defconst vkc_LateGame_FishHunt_MaxDropDistance             -1)


; ---- Goals ----

(defconst vkg_GameTimestamp 1)
(defconst vkg_LoopCount 2)
(defconst vkg_LoopTimeMs 3)


; ---- Rules ----

(defrule
	(true)
=>
    (set-strategic-number sn-do-not-scale-for-difficulty-level  vkc_True)
    (set-strategic-number sn-initial-exploration-required       0)
    (set-strategic-number sn-cap-civilian-builders              200)
    (set-strategic-number sn-cap-civilian-explorers             0)
    (set-strategic-number sn-number-explore-groups              1)
    (set-strategic-number sn-total-number-explorers             1)
	(set-strategic-number sn-consecutive-idle-unit-limit        1)
    (set-strategic-number sn-zero-priority-distance             255)
    (set-strategic-number sn-enable-offensive-priority          vkc_True)
    (set-strategic-number sn-enable-patrol-attack               vkc_True)
	(set-strategic-number sn-scale-minimum-attack-group-size    0)
	(set-strategic-number sn-task-ungrouped-soldiers            vkc_False)
    (set-strategic-number sn-dropsite-separation-distance       3)
    (set-strategic-number sn-allow-adjacent-dropsites           vkc_True)
    (set-strategic-number sn-enable-boar-hunting                1) ; 0 for deers only, 1 for deers and boars, 2 for boars only
    (set-strategic-number sn-maximum-wood-drop-distance         vkc_EarlyGame_WoodFoodGoldStone_MaxDropDistance)
	(set-strategic-number sn-maximum-food-drop-distance         vkc_EarlyGame_WoodFoodGoldStone_MaxDropDistance)
	(set-strategic-number sn-maximum-gold-drop-distance         vkc_EarlyGame_WoodFoodGoldStone_MaxDropDistance)
	(set-strategic-number sn-maximum-stone-drop-distance        vkc_EarlyGame_WoodFoodGoldStone_MaxDropDistance)
    (set-strategic-number sn-maximum-fish-boat-drop-distance    vkc_EarlyGame_FishHunt_MaxDropDistance)
    (set-strategic-number sn-maximum-hunt-drop-distance         vkc_EarlyGame_FishHunt_MaxDropDistance)
	(disable-self)
)



(defconst vkc-max-villagers 150)
(defconst vkc-dark-age-villagers-count-for-lumber-camp 7)
(defconst vkc-dark-age-villagers-count-for-mill 11)



(defrule
    (can-research feudal-age)
=>
    (research feudal-age)
)



(defrule
    (civilian-population < vkc-max-villagers)
    (can-train villager)
=>
    (train villager)
)



;#load-if-defined HUN-CIV        ; huns do not need houses
;#else
(defrule
    (population-headroom > 0)   ; difference between population cap and current population
    (housing-headroom < 2)      ; empty free spaces for new population
    (can-build house)
=>
    (build house)
)
;#end-if



(defrule
    (civilian-population >= vkc-dark-age-villagers-count-for-lumber-camp)
    (building-type-count-total lumber-camp == 0)
    (can-build lumber-camp)
=>
    (build lumber-camp)
    (set-strategic-number sn-food-gatherer-percentage 80)
    (set-strategic-number sn-wood-gatherer-percentage 20)
)



(defrule
    (civilian-population >= vkc-dark-age-villagers-count-for-mill)
    (building-type-count-total mill == 0)
    (can-build mill)
=>
    (build mill)
)



(defrule
    (true)
    =>
    (up-get-precise-time 0 vkg_GameTimestamp)
    (up-modify-goal vkg_LoopCount c:+ 1)
    (up-get-precise-time vkg_GameTimestamp vkg_LoopTimeMs)
)



(defrule
    (up-compare-const vkc_DebugLog_LoopData == vkc_True)
    =>
    ;(up-chat-data-to-all "Game timestamp: %d"   g: vkg_GameTimestamp)
    (up-chat-data-to-all "Loop count: %d"       g: vkg_LoopCount)
    (up-chat-data-to-all "Loop time (ms): %d"   g: vkg_LoopTimeMs)
)
